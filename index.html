<style>
	html,
	body {
		padding: 0;
		margin: 0;
	}

	#canvas {
		position: fixed;
		left: 50%;
		top: 50%;
		transform: translate(-50%, -50%);
	}
</style>
<canvas id="canvas"></canvas>
<script type="x" id="demo-vs">#version 300 es
layout(location = 0) in vec3 aPos;
layout(location = 1) in vec3 aNormal;
layout(location = 2) in vec2 aTexCoordinate;
layout(location = 3) in mat4 aModel;

uniform mat4 uView;
uniform mat4 uProjection;
uniform mat4 uLightMatrix;

out vec3 vNormal;
out vec4 vPos;
out vec2 vTexCoordinate;
out vec4 vLightPos;
out mat4 vView;

void main() {
	mat4 world = uView * aModel;
	vec4 pos = vec4(aPos, 1);
	gl_Position = uProjection * world * pos;

	vPos = world * pos;
	vNormal = mat3(world) * aNormal;
	vTexCoordinate = aTexCoordinate;
	vLightPos = uLightMatrix * aModel * pos;
	vView = uView;
}
</script>
<script type="x" id="demo-fs">#version 300 es
precision lowp float;

in vec3 vNormal;
in vec4 vPos;
in vec2 vTexCoordinate;
in vec4 vLightPos;
in mat4 vView;

layout(location = 0) out vec4 FragColor;
layout(location = 1) out vec4 BrightColor;

uniform samplerCube skybox;

struct Materia{
	sampler2D diffuse;
	sampler2D specular;
	float shininess;
};

struct Light{
	vec3 ambient;
	vec3 specular;
	vec3 position;
};

uniform Materia materia;

#define LIGHT_COUNT 2

uniform Light lights[LIGHT_COUNT];
vec3 calcDotLight(
	Light light,
	Materia materia,
	vec3 pos,
	vec3 lightPos,
	vec3 normal,
	vec2 tex);

void main(){
	vec2 TEXEL=vec2(1./512.,1./512.);
	
	vec3 pos=vPos.xyz/vPos.w;
	
	vec3 normal=normalize(vNormal);
	
	vec3 lightRes = vec3(0);
	for(int i=0;i<LIGHT_COUNT;i++){
		Light light = lights[i];
		vec4 lightPos = mat4(vView) * vec4(light.position, 1);
		lightRes += calcDotLight(light, materia, pos, lightPos.xyz, normal, vTexCoordinate);
	}

	vec3 view = vec3(0, 0, 1);
	vec3 reflectPos = -1.0 * reflect(view, normal);

	FragColor = texture(skybox, reflectPos);

	FragColor.xyz *= lightRes;

	// 检查结果值是否高于某个门槛，如果高于就渲染到高光颜色缓存中
	float brightness = dot(FragColor.xyz, vec3(0.2126, 0.7152, 0.0722));
	if(brightness > 1.0){
			BrightColor = FragColor;
	} else {
			BrightColor = vec4(0.0, 0.0, 0.0, 1.0);
	}
}

vec3 calcDotLight(
	Light light,
	Materia materia,
	vec3 pos,
	vec3 lightPos,
	vec3 normal,
	vec2 texCoord
){
	//考虑光强度衰减
	float distance=length(lightPos-pos);
	float attenuation=1./(1.+.09*distance+.032*(distance*distance));
	
	vec3 lightDirReverse= normalize(lightPos - pos);

	//漫反射光
	float diffuse = max(dot(lightDirReverse, normal), 0.5);
	vec3 ambient = texture(materia.diffuse, texCoord).xyz * (light.ambient * diffuse);
	
	//镜面光
	float pxPerW = 1.0 / 512.0;
	float pxPerH = 1.0 / 512.0;
	vec4 offsetX = vec4(1, -1, 1, -1);
	vec4 offsetY = vec4(1, -1, -1, 1);
	vec3 viewDir = normalize(vec3(0, 0, -1) - pos);
	vec3 halfVector = normalize(viewDir + lightDirReverse);
	vec4 specular = texture(materia.specular, texCoord);
	for (int i = 0; i < 4; i++) {
		specular.xyz += texture(materia.specular, texCoord + vec2(offsetX[i], offsetY[i])).xyz;
	}
	specular.xyz /= 4.0;
	specular.xyz *= pow(max(dot(halfVector, normal), 0.0), materia.shininess);

	return ambient + specular.xyz;
}
</script>
<script type="x" id="skybox-vs">#version 300 es
layout(location=10) in vec2 aPosition;
out vec2 vPosition;

void main() {
	gl_Position = vec4(aPosition, 1.0, 1.0);
	vPosition = aPosition;
}
</script>
<script type="x" id="skybox-fs">#version 300 es
precision lowp float;

uniform samplerCube uSkyBox;
uniform mat4 uCamera;
in vec2 vPosition;
layout (location = 0) out vec4 FragColor;
layout (location = 1) out vec4 BrightColor;

void main() {
	vec4 position = uCamera * vec4(vPosition, 1., 1.);
	FragColor = texture(uSkyBox, position.xyz / position.w);
		// 检查结果值是否高于某个门槛，如果高于就渲染到高光颜色缓存中
	float brightness = dot(FragColor.xyz, vec3(0.2126, 0.7152, 0.0722));
	if(brightness > 1.0){
			BrightColor = vec4(FragColor);
	} else {
			BrightColor = vec4(0.0, 0.0, 0.0, 0.0);
	}
}
</script>
<script type="x" id="bloom-vs">#version 300 es
layout(location = 10) in vec2 aPosition;
layout(location = 11) in vec2 aCoord;

out vec2 vCoord;

void main() {
	gl_Position = vec4(aPosition, 0.9, 1.0);
	vCoord = aCoord;
}
</script>
<script type="x" id="bloom-fs">#version 300 es
precision lowp float;

uniform vec3 color;
uniform sampler2D tex;

in vec2 vCoord;
out vec4 FragColor;

float weight[5] = float[](
	0.2270270270, 
	0.1945945946, 
	0.1216216216, 
	0.0540540541,
	0.0162162162);
uniform int horizontal;

void main() {
	vec2 offset = vec2(1.0 / float(textureSize(tex, 0)));
	vec3 result = texture(tex, vCoord).rgb * weight[0];
	if (horizontal == 1) {
		for (int i=1; i < 5; i++) {
			result += texture(tex, vCoord + vec2(offset.x * float(i), 0)).rgb * weight[i];
			result += texture(tex, vCoord - vec2(offset.x * float(i), 0)).rgb * weight[i];
		}
	} else {
		for (int i=1; i < 5; i++) {
			result += texture(tex, vCoord + vec2(0, offset.y * float(i))).rgb * weight[i];
			result += texture(tex, vCoord - vec2(0, offset.y * float(i))).rgb * weight[i];
		}
	}

	FragColor = vec4(result, 1);
}
</script>
<script type="x" id="hdr-vs">#version 300 es
layout(location = 10) in vec2 aPosition;
layout(location = 11) in vec2 aCoord;
out vec2 vCoord;

void main() {
	gl_Position = vec4(aPosition, 0, 1);
	vCoord = aCoord;
}
</script>
<script type="x" id="hdr-fs">#version 300 es
precision lowp float;
in vec2 vCoord;
out vec4 FragColor;

uniform sampler2D bloomTex;
uniform sampler2D hdrTex;

void main() {
	vec3 bloomColor = texture(bloomTex, vCoord).rgb;
	vec3 hdrColor = texture(hdrTex, vCoord).rgb;

	vec3 result = hdrColor + bloomColor;

	const float gamma = 2.2;
	// 曝光色调映射
	vec3 mapped = vec3(1.0) - exp(-result * 1.0);
	// Gamma校正
	mapped = pow(mapped, vec3(1.0 / gamma));
	FragColor = vec4(mapped, 1.0);
}
</script>
<!-- 矩阵运算库 -->
<script src="https://cdn.bootcdn.net/ajax/libs/gl-matrix/2.8.1/gl-matrix-min.js"></script>

<script src="./index.js"></script>